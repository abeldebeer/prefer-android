apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }
    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {
    compile deps.guava_preconditions

    testCompile deps.junit
}

// this task builds the default sources jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// this task builds a jar containing the test classes, so other modules can use them
task testClassesJar(type: Jar, dependsOn: 'compileDebugUnitTestSources') {
    from 'build/intermediates/classes/test/debug'
    archiveName "${baseName}-tests.jar"
}

configurations {
    tests
}

artifacts {
    archives sourcesJar
    tests testClassesJar
}
